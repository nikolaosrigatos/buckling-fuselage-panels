!Units: [N], [Nmm], [N/mm^2 = MPa], [N/mm]
FINISH
/CLEAR
/FILENAME,XXX
/TITLE, Flat plate compression

!White canvas for result presentation
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15

!Remove triad and min-max for result presentation
/VIEW,1,1,1,1
/TRIAD,OFF
/PLOPTS,MINM,OFF

!Remove date & time
/PLOPTS,DATE,0
/PLOPTS,LOGO,1

!Section color view on elements
/PNUM,SEC,1
/NUMBER,1

/PREP7
!######################################################################################################
 
b = 120       !Plate length
AR = 4        !Aspect ratio
a = AR*b	  !Plate width
t = 2         !Plate thickness

E1 = 70E3       !Young's modulus of elasticity
poiss = 0.316   !Poisson ratio

imp = 0.05
elemsize = 5

!######################################################################################################

!Material Properties
MP,EX,1,E1		    !Attribute Young's Modulus at Material 1
MP,NUXY,1,poiss		!Attribute Poisson ratio at Material 1

!Element type and properties
ET,1,SHELL181		!Create SHELL181 Element Type 1
SECTYPE,1,SHELL
SECDATA,t		    !Attribute thickness t at Element Type 1

!Geometry
K,1			    !Create a keypoint at (0,0,0)
K,2,a			!Create a keypoint at (a,0,0)
K,3,a,b			!Create a keypoint at (a,b,0)
K,4,0,b			!Create a keypoint at (0,b,0)
A,1,2,3,4		!Defines a rectangular area by connecting keypoints 1,2,3,4

!Meshing
ESIZE,elemsize       !Element sizing
AMESH,ALL		!Mesh all areas with defined sizing
EPLOT

!######################################################################################################
!Boundary Conditions	

NSEL,S,LOC,X,0    !Left and right side - Simply supported
NSEL,A,LOC,X,a
D,ALL,UZ
ALLSEL 

NSEL,S,LOC,Y,0    !Top and bottom side - Simply supported
NSEL,A,LOC,Y,b
D,ALL,UZ 
ALLSEL

NSEL,S,LOC,X,0    !Restrain left edge at X direction
D,ALL,UX 
ALLSEL
SAVE

NSEL,S,LOC,X,a    !Couple X DOF of right side (for uniform uniaxial compression)
*GET,minrightnode,NODE,0,NUM,MIN 
CP,NEXT,UX,ALL
ALLSEL 
 
NSEL,S,LOC,X,a/2   !Rigid body motion BC
NSEL,R,LOC,Y,b/2
D,ALL,UY
ALLSEL 

NSEL,S,LOC,Y,0     !Unloaded top edge to remain straight
CP,NEXT,UY,ALL 
ALLSEL 

NSEL,S,LOC,Y,b     !Unloaded bottom edge to remain straight
CP,NEXT,UY,ALL 
ALLSEL

D,minrightnode,UX,-3

!######################################################################################################
   
/SOLU
ANTYPE,STATIC             !Static analysis
PSTRES,ON                 !Calculate prestress effects
SOLVE
FINISH

/SOLU
ANTYPE,BUCKLE             !Buckling analysis
BUCOPT,LANB,1             !Use Block Lanczos method, 1 mode
MXPAND,1                  !Expand 1 mode shape
OUTPR,NSOL,ALL
SOLVE
FINISH

/POST1
SET,1,1
PLNSOL,U,SUM

/PREP7 
UPGEOM,imp,1,1,XXX,rst

/SOLU
NCNV,0 
ANTYPE,STATIC 
NLGEOM,ON       !Non-linear geometry
TIME,3          !Time at end of load step
AUTOTS,ON       !Auto timestepping on
NSUBST,50,150,50      !Number of substeps
NEQIT,200       !Max number of iterations
!CNVTOL,STAT 
RESCONTROL,DEFINE,ALL,1,
OUTRES,ALL,ALL 
SOLVE
FINISH 

/POST26
NUMVAR,300
NSOL,4,minrightnode,U,X,Displacement  !Read displacement results for right edge
RFORCE,5,minrightnode,F,X,Force       !Read reaction force results for right edge
ABS,6,4,,,Displacement,,,1
ABS,7,5,,,Force,,,1/1000          !Divide by 1000 to get results to kN
/AXLAB,X,Force
/AXLAB,Y,Displacement
XVAR,6
PLVAR,7                              !Load - displacement plot
